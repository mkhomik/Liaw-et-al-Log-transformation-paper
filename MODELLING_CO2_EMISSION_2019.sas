* FILE NAME: MODELLING_CO2_EMISSION_2019.SAS;
* BECAUSE LOG-TRANSFORMING THE DEPENDENT VARIABLE OF A REGRESSION MODEL
* RESULTS IN A SYSTEMATICALLY BIASED UNDER-PREDICTION PROBLEM, THIS
* PROGRAM DEMOSTRATES HOW YOU CAN ESTIMATE THE UNKNOWN COEFFICIENTS OF 
* YOUR REGRESSION MODEL, WITHOUT LOG-TRANSFORMING YOUR DEPENDENT VARIABLE;
* IN THIS DEMONSTRATION, WE USE THE GAUSSIAN-GAMMA MODEL TO PREDICT SOIL 
* CO2 EMISSION BY (1) SOIL TEMPERATURE AND (2) SOIL MOISTURE, USING THE 
* NONLINEAR LEAST-SQUARES METHOD; 
* THE ITERATIVE ALGORITHM FOR THIS METHOD IS DONE IN "NNL_REG_ROBUST_MODULE.SAS", 
* WHICH IS WRITTEN IN SAS/IML;

* PLEASE SPECIFY THE PATH TO THE FOLDER OF (1)THE RAW DATA FILE AND THE AND 
* (2) THE SAS MODULE FOR NONLINEAR LEAST-SQUARES ESTIMATION; 
LIBNAME SD 'D:\_SAS_DEMO_2019';
PROC MEANS DATA= SD.RAW_DATA_2014_S_ONTARIO N NMISS MIN MAX MEAN STD CV RANGE;
TITLE "SUMMARY OF THE RAW DATA: HALF-HOURLY RECORDS IN THE YEAR 2014.";
TITLE2 "RS = CO2 EMISSION FROM SOIL (µMOL/M^2/S).";
TITLE3 "TS_5CM = TEMPERATURE OF SOIL AT THE DEPTH OF 5 CM (DEGREE C).";
TITLE4 "MS_30CM = MOISTURE OF SOIL AT THE DEPTH OF 30CM (VOLUME/VOLUME)."; 
RUN;
DATA F1;
TITLE "DATA CONTAINING ORIGINAL AND TRANSFORMED VARIABLES FOR APPLYING THE GAUSSIAN-GAMMA MODEL:";
  SET SD.RAW_DATA_2014_S_ONTARIO; 
* CREATE THE SQUARE OF TEMPERATURE;
  SQTs_5cm = Ts_5cm * Ts_5cm;
* CHANGE THE UNIT OF MOISTURE FROM PROPORTION TO PERCENTAGE;
  Ms_30cm = Ms_30cm * 100;
* LOG TRANSFORMING SOIL MOISTURE;
  LNMs_30cm = LOG(Ms_30cm);
* CREATE THE COLUMN REPRESENTING THE INTERCEPT;
INTERCEPT=1;
* YOU MUST NOT FORGET THE INTERCEPT, UNLESS YOU DO NOT WANT THE INTERCEPT TO BE PART OF YOUR MODEL!!!;
PROC MEANS DATA=F1 N NMISS MIN MAX MEAN STD CV RANGE;
RUN;
***********************************************************************;
TITLE "GAUSSIAN_GAMMA MODEL FOR THE DATA OF THE TEMPERATE FOREST. ";
TITLE2 "DEPENDENT VAR (RS) = SOIL CO2 EMISSION ( µMOL CO2/m^2/s ).";
TITLE3 "TS_5CM = TEMPERATURE OF SOIL AT THE DEPTH OF 5 CM (DEGREE C).";
TITLE4 "MS_30CM = MOISTURE OF SOIL AT THE DEPTH OF 30CM (VOLUME/VOLUME*100%)."; 
TITLE5 "RESULT OF THE NONLINEAR ESTIMATION METHOD:";
* SPECIFY THE NAME OF THE DEPENDENT VARIABLE;
%LET DEP_VAR= RS;
* SPECIFY THE NAMES OF THE EXPLANATORY VARIABLES (DON'T FORGET THE INTERCEPT);
%LET INDEP= INTERCEPT  Ts_5cm  SQTs_5cm    Ms_30cm  LNMs_30cm  ;
DATA INPUT_FOR_MODEL;
* CREATE THE INPUT DATA MATRIX;
  SET F1  (KEEP= &DEP_VAR  &INDEP);
RUN;
PROC IML ;
use INPUT_FOR_MODEL;
%INCLUDE "D:\_SAS_DEMO_2019\NNL_REG_ROBUST_MODULE.SAS";
RUN  NNL_REG_ROBUST;
PROC MEANS DATA=Y_HAT_FILE N NMISS MIN MAX MEAN STD CV RANGE;
TITLE "THE SAS DATA SET 'Y_HAT_FILE' CONTAINS ALL VALUES OF THE PREDICTED CO2 EMISSION: DENOTED BY Y_HAT.";
TITLE2 "ITS SUMMARY STATISTICS ARE SHOWN HERE."; 
RUN;
*********** END OF THE PROGRAM *********************************************;
